import "film_simulation.pkl"

class ConversionProfile {
    hidden application = "XRFC"
    hidden version = "1.12.0.0"
    propertyGroup: PropertyGroup
}

class PropertyGroup {
    hidden device: String   // e.g. "X100V"
    hidden version: String  // e.g. "X100V_0200"
    hidden label: String    // e.g. "Fujicolor Superia 800", must match filename, i.e. "$label.FP1"

    /// ConversionProfiles are tied to camera bodies and need a serial number to be used with X Raw Studio
    serialNumber: String(length == 30)

    /// same as PropertyGroup.version, also sic: missing second i in "Conditon"
    tetherRAWConditonCode: String = version

    /// false in FP3 files
    editable: Boolean = true

    /// source raw filename in FP3 data
    sourceFileName: String = ""

    /// Unknown when this is used, probably in FP3
    fileError: String = "NONE"

    /// may be enumerated (0, 90, 180)?
    rotationAngle: Int = 0

    /// need to verify if this applies to all bodies
    structVer: Int = 65536

    /// IOPCode array
    iopCode: IopCode

    /// not clear what this does
    shootingCondition: *"OFF"|"ON"

    /// unclear if this can be set to anything else
    fileType: String = "JPG"

    /// Output JPEG size and aspect ratio
    imageSize:
        *"L3x2"
        |"L16x9"
        |"L1x1"
        |"M3x2"
        |"M16x9"
        |"M1x1"
        |"S3x2"
        |"S16x9"
        |"S1x1"

    /// JPEG image quality
    imageQuality: *"Fine"|"Normal"

    /// Push/pull processing
    exposureBias: Thirds

    /// DR-Auto is a camera setting, not a conversion setting, and cannot be used here
    dynamicRange: Int(isValidDynamicRange) = 100

    /// D Range Priority: [Off, Weak, Strong] (cannot be set to Auto)
    wideDRange: *"0"|"P1"|"P2"
    filmSimulation: film_simulation.FilmSimulation   // simulations are defined by the camera
    blackImageTone: Int8 = 0         // Monochromatic Color WC (Warm/Cool) component. may be device-specific
    monochromaticColor_RG: Int8 = 0  // Monochromatic Color MG (Magenta/Green) component
    grainEffect: Strength = "OFF"
    grainEffectSize: "LARGE"|*"SMALL"
    chromeEffect: Strength = "OFF"     // Color Chrome Effect
    colorChromeBlue: Strength = "OFF"  // Color Chrome FX Blue
    smoothSkinEffect: "ON"|*"OFF"
    wbShootCond: "ON"|*"OFF"
    whiteBalance:
        "Auto"           // Auto White Balance
        | "Custom1"      // Custom 1
        | "Custom2"      // Custom 2
        | "Custom3"      // Custom 3
        | "Temperature"  // Temperature
        | "Daylight"     // Daylight
        | "Shade"        // Shade
        | "FLight1"      // Fluorescent Light-1
        | "FLight2"      // Fluorescent Light-2
        | "FLight3"      // Fluorescent Light-3
        | "Incand"       // Incendescent
        | "UWater"       // Underwater
    wbShiftR: Int8
    wbShiftB: Int8
    wbColorTemp: WBColorTemp
    highlightTone: Float
    shadowTone: Float  // these were Ints up until the X-Trans V
    color: Int8        // ignored with b&w
    sharpness: Int8
    noisReduction: Int8  // spelled as such
    clarity: Int8
    lensModulationOpt: *"ON"|"OFF"
    colorSpace: *"sRGB"|"AdobeRGB"
    hdr:  // any enabled mode disables DR, DR-P, Tone Curve, and Clarity
        *""
        | "200"
        | "400"
        | "800"
        | "800PLUS"
    digitalTeleConv: "ON"|*"OFF"
    portraitEnhancer: String = ""
}

const local isValidDynamicRange = (value) ->
    Set(100, 200, 400).contains(value)

/// A fractional value incremented in thirds.
/// The output format is /[PM]\dP\d\d/
/// P/M seems to indicate Plus or Minus. Zero is signed.
/// The first digit indicates whole stops, and the second digits indicate thirds in decimal.
///
/// ex.
/// M2P00 = -2
/// P1P67 = +1 2/3
class Thirds {
    // TODO string renderer
    wholeComponent: Int(isBetween(-2, 3)) = 0
    fraction: Int(isValidThird) = 0
}

const local isValidThird = (num) -> Set(0, 33, 67).contains(num)

class WBColorTemp {  // TODO string renderer (add K)
    value: Int(isBetween(2500, 10000), this % 10 == 0) = 10000
}

typealias Strength = "STRONG"|"WEAK"|"OFF"

/// bytearray containing color space, processor version, and other (unknown) options
/// AND with 0xFF000000 to get color space (FF = sRGB, FA = Adobe RGB)
/// AND with 0x00FFFF00 to get the processor version
typealias IopCode = // these are hex values but rendered as strings in xml
    "FF129502"    // sRGB, X-Processor Pro2: X-T2
    | "FF129504"  // sRGB, X-Processor Pro2: X100F
    | "FF129506"  // sRGB, X-Processor Pro2: X-H1
    | "FF159501"  // sRGB, X-Processor IV: X-T3
    | "FF159504"  // sRGB, X-Processor IV: X100V
    | "FF159505"  // sRGB, X-Processor IV: X-T4
    | "FA179502"  // Adobe RGB, X-Processor V: X-T5
