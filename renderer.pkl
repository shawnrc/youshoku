import "pkl:xml"

import* "./out/bw/*.pkl" as renderedRecipes
import "schemas/conversion_profile.pkl"
import "schemas/X100VRecipe.pkl"
import "schemas/film_simulation.pkl" as Simulations
import "schemas/whiteBalance.pkl" as WhiteBalance

recipes: Listing<conversion_profile.ConversionProfile> = new {
    for (recipe in renderedRecipes) {
        new conversion_profile.ConversionProfile {
            propertyGroup {
                device = "X100V"
                version = "X100V_0200"
                label = recipe.name
                serialNumber = read("env:SERIAL_NUMBER")
                // tetherRAWConditonCode = "X100V_0200"
                iopCode = "FF159504"
                dynamicRange = if (recipe.dynamicRange.contains("Auto")) 100 else recipe.dynamicRange.drop(2).toInt()
                filmSimulation = Simulations.lookup[recipe.filmSimulation]
                blackImageTone = recipe.monochromaticColorWC
                monochromaticColor_RG = recipe.monochromaticColorMG
                grainEffect = recipe.grainEffect.toUpperCase()
                grainEffectSize = recipe.grainSize.toUpperCase()
                chromeEffect = recipe.colorChromeEffect.toUpperCase()
                colorChromeBlue = recipe.colorChromeFxBlue.toUpperCase()
                whiteBalance = WhiteBalance.lookup[recipe.whiteBalance]
                wbShiftR = recipe.wbShiftRed
                wbShiftB = recipe.wbShiftBlue
                highlightTone = recipe.highlight?.toFloat() ?? 0.0
                shadowTone = recipe.shadow?.toFloat() ?? 0.0
                color = recipe.color ?? 0
                sharpness = recipe.sharpness
                noisReduction = recipe.noiseReduction
                clarity = recipe.clarity
            }
        }
    }
}

output {
    renderer = new xml.Renderer {
        converters {
            [conversion_profile.ConversionProfile] = (it) -> (xml.Element("ConversionProfile")) {
                attributes {
                    ["application"] = it.application
                    ["version"] = it.version
                }
                xml.Inline(it)
            }
            [conversion_profile.PropertyGroup] = (it) -> (xml.Element("PropertyGroup")) {
                attributes {
                    ["device"] = it.device
                    ["version"] = it.version
                    ["label"] = it.label
                }
                xml.Inline(it)
            }
            [conversion_profile.Thirds] = (it) -> (xml.Element("exposureBias")) {
                (if (it.wholeComponent >= 0) "P" else "M") + "\(it.wholeComponent)P\(if (it.fraction < 10) "0" else "")\(it.fraction)"
            }
            [conversion_profile.WBColorTemp] = (it) -> (xml.Element("wbColorTemp")) {
                "\(it.value)K"
            }
        }
    }
}
